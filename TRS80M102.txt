100 REM BASIC Month 5: Crisps Tunes
110 REM http://reddit.com/r/RetroBattlestations
120 REM written by FozzTexx
130 REM modified for TRS-80 Model 102 by u/SDGOL
140 PRINT "r/retrobattlestations"
142 PRINT "BASIC Month 5 Challenge"
144 PRINT DATE$+"  "+TIME$
146 PRINT "TRS-80 Model 102 port"
148 PRINT "U/SDGOL"
200 REM Initialize variables
210 DIM M(7):FOR I=1 TO 7:READ M(I):NEXT I
220 SH=0:RH=0:OM=4:DIM A(12 * OM)
230 GOSUB 900
240 DIM HZ(72):FOR I=1 TO 72:READ HZ(I):NEXT I
300 REM Loop through data and play song
310 READ S$:SL=LEN(S$):IF SL=0 THEN END
320 FOR I=1 TO SL
330 OC=4:DI=0:MU=0:AC=0
340 IF I>SL THEN NEXT I
350 N$ = MID$(S$,I,1)
360 IF N$=" " THEN NEXT I
370 IF N$="!" THEN GOSUB 910:NEXT I
380 IF N$="^" THEN AC=1:SH=SH+1:I=I+1:GOTO 340
390 IF N$="-" THEN AC=1:SH=SH-1:I=I+1:GOTO 340
400 IF N$="=" THEN AC=1:SH=0:I=I+1:GOTO 340
410 IF N$>="a" AND N$<="g" THEN N$=CHR$(ASC(N$)-32):OC=OC+1
420 IF N$>="A" AND N$<="G" THEN C=ASC(N$):C=C-64:NT=M(C)
500 REM Keep reading modifier after the note until there's another note
510 I=I+1:IF I>SL THEN GOTO 610
520 M$=MID$(S$,I,1)
530 IF M$="," THEN OC=OC-1:GOTO 510
540 IF M$="'" THEN OC=OC+1:GOTO 510
550 IF M$>="0" AND M$<="9" THEN C=ASC(M$):MU=MU*10+C-48:GOTO 510
560 IF M$="/" THEN DI=1:GOTO 510
570 IF M$="<" THEN RH=1:RM=0.5:GOTO 510
580 IF M$=">" THEN RH=1:RM=1.5:GOTO 510
590 I=I-1
600 REM Play note
610 IF NT<0 THEN NT=NT+12:OC=OC-1:GOTO 610
620 IF AC THEN AI=NT+13:A(AI)=SH:SH=0
630 IF AC AND AI<A1 THEN A1=AI
640 IF AC AND AI>A2 THEN A2=AI
650 SH=A(NT+13):NT=NT+SH
660 IF DI AND MU=0 THEN MU=2
670 IF DI THEN MU=1/MU
680 IF MU=0 THEN MU=1
690 IF RH THEN MU= MU*RM
700 DU=INT(MU)*6
710 F$="":IF SH<>0 THEN F$="#":IF SH<0 THEN F$="$"
720 PRINT N$;F$;OC;"=";MU;" ";
730 REM Note (NT) is numeric note, 0 is middle C.
732 J=NT+1+(12*(OC-1))
734 FQ=HZ(J)
740 SOUND FQ,DU
750 IF RH THEN RH=RH+1:RM=2-RM:IF RH=3 THEN RH=0
800 NEXT I
810 GOTO 310
900 REM clear all accidentals
910 FOR AD = A1 TO A2:A(AD) = 0:NEXT AD
920 A1 = 12 * OM:A2 = 0
930 RETURN
1000 DATA 9,11,0,2,4,5,7
1005 REM musical scale frequencies
1010 DATA 0,0,0,15800,14912,14064
1020 DATA 13284,12538,11836,11172
1030 DATA 10544,9952
1040 DATA 9394,8866,8368,7900,7457
1050 DATA 7032,6642,6269,5918,5586
1060 DATA 5272,4968
1070 DATA 4697,4433,4184,3950,3728
1080 DATA 3516,3321,3134,2954,2793
1090 DATA 2636,2484
1100 DATA 2348,2216,2092,1975,1864
1110 DATA 1758,1660,1567,1479,1396
1120 DATA 1318,1224
1130 DATA 1171,1103,1045,987,932,873
1140 DATA 830,783,739,693,659,622
1150 DATA 587,554,523,493,466,439,415
1160 DATA 0,0,0,0,0
1480 REM exclamation point has been substituted for pipe,
1490 REM hyphen has been substituted for underscore
1500 DATA "G,G, ! ^G,G, A,A, G,G, ! G,G, ^G,G, A,2 ! G,^G, A,2 E,2 ! E,2 E,2 E,8"
1510 DATA "! G,G, ^G,G, A,A, ! ^G,G, =G,G, ^G,G, ! A,2 G,^G, A,2 ! F,2 F,2 F,2"
1520 DATA "! F,8 ! F,2 F,2 ! F,2 F,2 F,G, ! A,B,5 ! E,2 A,2 E,2 ! A,2 E,F, G,A,5"
1530 DATA "! D,2 ! E,4 ^F,2 ! A,2 B,4 ! A,-B, =B,2 G,2 ! G,2 G,2 G,8"
1540 DATA "! G,G, ^G,G, A,A, ! ^G,G, =G,G, ^G,G, ! A,2 G,^G, A,2 ! E,2 E,2 E,2"
1550 DATA "! E,8 ! E,E, ^D,D, ! E,2 ^F,2 ^G,2 ! E,2 ^F,2 ^G,2 ! A,2 ^G,2 A,2"
1560 DATA "! B,4< C4 ! B,C D2 ! A,2 A,2 A,2 ! D2<A,2 A,2 ! B,2 C2<G,2"
1570 DATA "! G,2 A,2 C2<G,2 ! G,2 ^G,2 ! A,2 ^G,2<A,2 ! -B,2 =B,B, B,-B,"
1580 DATA "! B,2 G,2 C16"
1590 DATA ""
